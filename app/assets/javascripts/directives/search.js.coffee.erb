angular.module('BoxApp').directive 'search', ['$timeout', '$injector', ($timeout, $injector) ->
  setupTimer = true

  restrict: 'E'
  replace: true

  scope:
    onSelect: '='
    searchFor: '@'
    property: '='
    placeholder: '@'

  link: (scope, element, attrs) ->
    scope.$watch 'property', ->
      $timeout.cancel timer if timer?
      return setupTimer = true unless setupTimer
      timer = $timeout( ->
        if scope.property
          scope.showResults = true
          scope.search()
        else
          scope.showResults = false
      , 600)

  controller: ['$scope', '$element', ($scope, $element) ->
    $scope.search = ->
      $injector.get($scope.searchFor).search($scope.property).then (result) ->
        $scope.items = result

    $scope.click = (item) ->
      $scope.property = item.name
      setupTimer = false
      $scope.showResults = false
      $scope.onSelect? item
  ]

  templateUrl: '<%= asset_path "search.html" %>'
]